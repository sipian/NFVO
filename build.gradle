/*
 * Copyright (c) 2016 Open Baton (http://www.openbaton.org)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */


buildscript {     //api-doc
    dependencies {
        classpath("org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.9")
    }
}


plugins {
    id 'net.researchgate.release' version '2.3.4'
    id "nebula.os-package" version "2.2.6"
    id "org.asciidoctor.convert" version "1.5.2"    //api-doc
    id 'com.github.sherter.google-java-format' version '0.3.2'
    // For the managing of the Licences
    id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

apply from: 'openbaton-libs/gradle/gradle/git.helpers.gradle'
apply from: 'openbaton-libs/gradle/gradle/command.line.helper.gradle'

/**
 * Project settings
 */
project.ext {
    //------------------------------------------------//
    /**
     * Spring Related Variables
     */
    springVersion = '4.2.0.RELEASE'
    springBootVersion = '1.2.5.RELEASE'
    //------------------------------------------------//
    //------------------------------------------------//
    /**
     * Application Related Version
     */
    applicationVersion = version

    applicationRelease = isApplicationRelease()
    applicationDate = new Date()
    applicationRevision = getRevision()

    applicationVersionSnapshot = (!applicationRelease) ? ".${asUTC(applicationDate, 'yyyyMMdd')}.git${applicationRevision}" : ""
    applicationVersionFull = "${applicationVersion}${applicationVersionSnapshot}"
    //------------------------------------------------//
    //------------------------------------------------//
    /**
     * gradle Related Vars
     */
    mainClass = 'org.openbaton.nfvo.main.Application'
    //------------------------------------------------//
    /**
     * used for the api documentation
     */
    snippetsDir = file('api/build/api-documentation/generated-snippets')
    springRestdocsVersion = '1.0.0.RC1'
}

apply from: 'openbaton-libs/gradle/gradle/application.java.gradle'
apply from: 'openbaton-libs/gradle/gradle/run.application.java.gradle'
apply from: 'openbaton-libs/gradle/gradle/git.release.gradle'

apply from: 'gradle/gradle/packaging.nfvo.debian.gradle'
apply from: 'gradle/gradle/packaging.innosetup.gradle'
apply from: 'gradle/gradle/distribution.gradle'


repositories {
    mavenCentral()
}

dependencies {
    compile project(':main')
}

asciidoctorj {
    version = '1.5.2'
}

/**
 * used for the api documentation
 */
asciidoctor {
    sourceDir 'api/src/test/java/org/openbaton/nfvo/core/tests/api/doc'
    attributes 'snippets': snippetsDir
    backends = ['html5', 'pdf']
    outputDir 'api/build/api-documentation'
    inputs.dir snippetsDir
    dependsOn test
}

test {  //api-doc
    outputs.dir snippetsDir
}

jar {   //api-doc
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") {
    }
}

build.dependsOn verifyGoogleJavaFormat
allprojects {
    jar {
        manifest {
            attributes("Implementation-Version": version)
        }
    }
}
compileJava.dependsOn tasks.googleJavaFormat
